module Eventful.EventBus
  ( synchronousEventBusWrapper
  , storeAndPublishEvents
  ) where

import Eventful.Store.Class
import Eventful.UUID

-- | In-memory synchronous event bus. This takes a list of event handlers and
-- wraps an event store. When events are stored the handlers are called with
-- the new events.
synchronousEventBusWrapper
  :: (Monad m)
  => EventStore serialized m
  -> [EventStore serialized m -> UUID -> serialized -> m ()]
  -> EventStore serialized m
synchronousEventBusWrapper store handlers =
  let
    -- NB: We need to use recursive let bindings so we can put wrappedStore
    -- inside the event handlers
    handlers' = map ($ wrappedStore) handlers
    wrappedStore =
      EventStore
      { getLatestVersion = getLatestVersion store
      , getEvents = getEvents store
      , storeEvents = storeAndPublishEvents store handlers'
      }
  in wrappedStore

-- | Stores events in the store and them publishes them to the event handlers.
storeAndPublishEvents
  :: (Monad m)
  => EventStore serialized m
  -> [UUID -> serialized -> m ()]
  -> ExpectedVersion
  -> UUID
  -> [serialized]
  -> m (Maybe EventWriteError)
storeAndPublishEvents store handlers expectedVersion uuid events = do
  result <- storeEvents store expectedVersion uuid events
  case result of
    Just err -> return $ Just err
    Nothing -> do
      -- NB: If a handler stores events, then its events will be published
      -- before the events of the next handler. That is, we will be storing
      -- events generated by handlers in depth-first order.
      mapM_ (\handler -> mapM_ (handler uuid) events) handlers
      return Nothing
